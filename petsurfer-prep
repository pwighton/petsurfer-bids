#!/bin/tcsh -f
# petsurfer-prep - runs petprep from a user-friendly shell

if(-e $FREESURFER_HOME/sources.csh) then
  source $FREESURFER_HOME/sources.csh
endif

set VERSION = '$Id$';
set scriptname = `basename $0`

set sif = /autofs/vast/petsurfer/bids/containers/nipreps-petprep.sif 
if($#PETSURFER_PETPREP_SIF) then
  set sif = $PETSURFER_PETPREP_SIF
endif

set license = ()
set bidstop = ()
set participant = ()
set session = baseline
set outtop = ();
set outdir = ()
set fs_subjects_dir = /autofs/space/metropolis_001/users/greve/cox1/subjects2
set fs_subjects_dir = ()
set pvc = ()
set DoCifti = 0
set pvcpsf = 6

set ForceUpdate = 0
set threads = 1

set tmpdir = ();
set cleanup = 1;
set LF = ();

set inputargs = ($argv);
set PrintHelp = 0;
if($#argv == 0) goto usage_exit;
set n = `echo $argv | grep -e -help | wc -l` 
if($n != 0) then
  set PrintHelp = 1;
  goto usage_exit;
endif
set n = `echo $argv | grep -e -version | wc -l` 
if($n != 0) then
  echo $VERSION
  exit 0;
endif
goto parse_args;
parse_args_return:
goto check_params;
check_params_return:

set StartTime = `date`;
set tSecStart = `date '+%s'`;
set year  = `date +%Y`
set month = `date +%m`
set day   = `date +%d`
set hour   = `date +%H`
set min    = `date +%M`

mkdir -p $outdir/log
pushd $outdir > /dev/null
set outdir = `pwd`;
popd > /dev/null

if($#tmpdir == 0) then
  if(-dw /scratch)   set tmpdir = /scratch/tmpdir.petsurfer-prep.$$
  if(! -dw /scratch) set tmpdir = $outdir/tmpdir.petsurfer-prep.$$
endif
mkdir -p $tmpdir

# Set up log file
if($#LF == 0) set LF = $outdir/log/petsurfer-prep.Y$year.M$month.D$day.H$hour.M$min.log
if($LF != /dev/null) rm -f $LF
echo "Log file for petsurfer-prep" >> $LF
date  | tee -a $LF
echo "" | tee -a $LF
echo "setenv SUBJECTS_DIR $SUBJECTS_DIR" | tee -a $LF
echo "cd `pwd`"  | tee -a $LF
echo $0 $inputargs | tee -a $LF
ls -l $0  | tee -a $LF
echo "" | tee -a $LF
cat $FREESURFER_HOME/build-stamp.txt | tee -a $LF
echo $VERSION | tee -a $LF
uname -a  | tee -a $LF
echo "pid $$" | tee -a $LF
if($?PBS_JOBID) then
  echo "pbsjob $PBS_JOBID"  >> $LF
endif
if($?SLURM_JOB_ID) then
  echo SLURM_JOB_ID $SLURM_JOB_ID >> $LF
endif

#========================================================
if($#outtop) mkdir -p $outtop
set apptainerhome = /tmp/apptainer/$user/home
mkdir -p $apptainerhome
set cmd = (singularity run --containall \
    -B ${bidstop}:/bids_dataset_in \
    -B ${outdir}:/bids_dataset_out \
    -B ${workingdir}:/bids_dataset_work \
    -B ${tmpdir}:/tmp \
    -B ${license}:/.license:ro)
if($#fs_subjects_dir) set cmd = ($cmd -B ${fs_subjects_dir}:/fs_subjects)
set cmd = ($cmd \
    --pwd /bids_dataset_work \
    --home $apptainerhome \
    $sif /bids_dataset_in /bids_dataset_out participant \
    --skip_bids_validation --omp-nthreads $threads\
    --participant-label $participant --session-label $session\
    --fs-license-file /.license \
    --output-spaces MNI152NLin2009cAsym fsaverage fsnative)
if($DoCifti) set cmd = ($cmd  --cifti-output 91k)
if($#pvc) set cmd = ($cmd --pvc-tool petsurfer --pvc-method $pvc --pvc-psf $pvcpsf)
if($#fs_subjects_dir) set cmd = ($cmd --fs-subjects-dir /fs_subjects)

echo "" | tee -a $LF
echo "cd `pwd`"| tee -a $LF
echo $cmd| tee -a $LF
echo ""| tee -a $LF
fs_time $cmd | tee -a $LF
echo ""| tee -a $LF


#========================================================

# Cleanup
# if($cleanup) rm -rf $tmpdir

# Done
echo " " |& tee -a $LF
set tSecEnd = `date '+%s'`;
@ tSecRun = $tSecEnd - $tSecStart;
set tRunMin = `echo $tSecRun/60|bc -l`
set tRunMin = `printf %5.2f $tRunMin`
set tRunHours = `echo $tSecRun/3600|bc -l`
set tRunHours = `printf %5.2f $tRunHours`
echo "Started at $StartTime " |& tee -a $LF
echo "Ended   at `date`" |& tee -a $LF
echo "Petsurfer-Prep-Run-Time-Sec $tSecRun" |& tee -a $LF
echo "Petsurfer-Prep-Run-Time-Min $tRunMin" |& tee -a $LF
echo "Petsurfer-Prep-Run-Time-Hours $tRunHours" |& tee -a $LF
echo " " |& tee -a $LF
echo "petsurfer-prep Done" |& tee -a $LF
exit 0

###############################################

############--------------##################
error_exit:
echo "ERROR:"

exit 1;
###############################################

############--------------##################
parse_args:
set cmdline = ($argv);
while( $#argv != 0 )

  set flag = $argv[1]; shift;
  
  switch($flag)

    case "--o":
      if($#argv < 1) goto arg1err;
      set outdir = $argv[1]; shift;
      breaksw

    case "--b":
      if($#argv < 1) goto arg1err;
      set bidstop = $argv[1]; shift;
      breaksw

    case "--p":
      if($#argv < 1) goto arg1err;
      set participant = $argv[1]; shift;
      breaksw

    case "--sess":
    case "--ses":
      if($#argv < 1) goto arg1err;
      set session = $argv[1]; shift;
      breaksw

    case "--threads":
      if($#argv < 1) goto arg1err;
      set threads = $argv[1]; shift;
      breaksw

    case "--sdir":
      if($#argv < 1) goto arg1err;
      set fs_subjects_dir = $argv[1]; shift;
      set fs_subjects_dir = `getfullpath $fs_subjects_dir`
      breaksw
    case "--no-sdir":
      set fs_subjects_dir = ()
      breaksw

    case "--cifti":
      set DoCifti = 1
      breaksw
    case "--no-cifti":
      set DoCifti = 0
      breaksw

    case "--mg":
      set pvc = MG
      breaksw

    case "--sif":
      if($#argv < 1) goto arg1err;
      set sif = $argv[1]; shift;
      breaksw

    case "--force":
     set ForceUpdate = 1
     breaksw
    case "--no-force":
     set ForceUpdate = 0
     breaksw

    case "--log":
      if($#argv < 1) goto arg1err;
      set LF = $argv[1]; shift;
      breaksw

    case "--nolog":
    case "--no-log":
      set LF = /dev/null
      breaksw

    case "--tmp":
    case "--tmpdir":
      if($#argv < 1) goto arg1err;
      set tmpdir = $argv[1]; shift;
      set cleanup = 0;
      breaksw

    case "--nocleanup":
      set cleanup = 0;
      breaksw

    case "--cleanup":
      set cleanup = 1;
      breaksw

    case "--debug":
      set verbose = 1;
      set echo = 1;
      breaksw

    default:
      echo ERROR: Flag $flag unrecognized. 
      echo $cmdline
      exit 1
      breaksw
  endsw

end

goto parse_args_return;
############--------------##################

############--------------##################
check_params:

set license = $FREESURFER/.license
if(! -e $license) then
  set license = $FREESURFER/license
  if(! -e $license) then
    echo "ERROR: cannot find FreeSurfer license in $FREESURFER"
    exit 1
  endif
endif

if(! -e $sif) then
  echo "ERROR: cannot find $sif"
  exit 1
endif

if($#bidstop == 0) then
  echo "ERROR: must spec bidstop with --b"
  exit 1;
endif
if($#outdir == 0) then
  echo "ERROR: must spec outdir with --o"
  exit 1;
endif
if($#participant == 0) then
  echo "ERROR: must spec participant with --p"
  exit 1;
endif

# Must be outside of the bids tree
#set workingdir = `dirname $outdir`/working
set wd = `basename $outdir`
set workingdir = /space/metropolis/1/users/greve/bids-work/$wd
mkdir -p $workingdir

goto check_params_return;
############--------------##################

############--------------##################
arg1err:
  echo "ERROR: flag $flag requires one argument"
  exit 1
############--------------##################
arg2err:
  echo "ERROR: flag $flag requires two arguments"
  exit 1
############--------------##################

############--------------##################
usage_exit:
  echo ""
  echo "petsurfer-prep : BIDS app to run preprocessing for PETsurfer"
  echo " --b bidstopdir : BIDS folder, eg, downloaded from OpenNeuro"
  echo " --o outputdir : if not specified, then bdstopdir/derivative"
  echo " --p participant (eg, sub-PS19)"
  echo " --ses session : eg, baseline; default is $session"
  echo " --sdir FSDIR (also --no-sdir) : FreeSurfer SUBJECTS_DIR"
  echo " --mg : mueller-gaertner PVC"
  echo " --cifti : output HCP cifit maps"
  echo " --threads threads"
  echo " --sif petprep singularity container"
  echo ""

  if(! $PrintHelp) exit 1;
  echo $VERSION
  cat $0 | awk 'BEGIN{prt=0}{if(prt) print $0; if($1 == "BEGINHELP") prt = 1 }'
  singularity run $sif

exit 1;

#---- Everything below here is printed out as part of help -----#
BEGINHELP



